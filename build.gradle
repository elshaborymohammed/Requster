// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: "dependencies.gradle"

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.3'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    apply plugin: "maven-publish"

    def getVersionName = { ->
        return "0.0.1"
    }
    def github = new Properties()
    github.load(new FileInputStream(rootProject.file("github.properties")))

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/elshaborymohammed/Requster")
                credentials {
                    username = github['gpr.user'] ?: System.getenv("GPR_USER")
                    password = github['gpr.key'] ?: System.getenv("GPR_API_KEY")
                }
            }
        }

        publications {
            release(MavenPublication) {
                groupId "com.compact.requester"
                artifactId ARTIFACT_ID
                version getVersionName()
                artifact("$buildDir/libs/${ARTIFACT_ID}.jar")
            }
        }
    }
}